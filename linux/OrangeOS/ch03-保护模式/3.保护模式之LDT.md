# 保护模式 - LDT（Local Descriptor Table）

[toc]





## 代码示例

代码如下(`OrangeOS/Protect-Mode/c/protect_mode_test_c.asm`)：

```assembly
; ==========================================
; chat03 保护模式入门
; protect_mode_test_a.asm
; 编译方法 nasm protect_mode_test_a.asm -o protect_mode_test_a.bin
; ==========================================


%include "protect_mode.inc"						; 常量、宏等说明


org			0100h								; org 是 Origin 的缩写，伪指令，表示程序的起始地址
; 不是该程序在内存中的地址，而是当前程序使用的基地址

		jmp			LABEL_BEGIN


[SECTION	.gdt]
; GDT 声明代码段, GDT 表项
;									段基址,			   段界限,			属性
LABEL_GDT:			Descriptor			0,				   0,			  	   0	; 空描述符
LABEL_DESC_NORMAL:	Descriptor			0,			  0ffffh,		      DA_DRW	; Normal 描述符
LABEL_DESC_CODE32:	Descriptor			0,	SegCode32Len - 1,		DA_C + DA_32	; 非一致性代码段，32位
LABEL_DESC_CODE16:	Descriptor			0,			  0ffffh,				DA_C	; 非一致性代码段，16位
LABEL_DESC_DATA:	Descriptor			0,		 DataLen - 1,		  	  DA_DRW	; Data 数据段
LABEL_DESC_STACK:	Descriptor			0,		  TopOfStack,	 DA_DRWA + DA_32	; Stack 栈段，32 位
LABEL_DESC_TEST:	Descriptor	 0500000h,			  0ffffh,			  DA_DRW	; 测试段
LABEL_DESC_LDT:		Descriptor			0,		  LdtLen - 1,			  DA_LDT	; LDT 段，LDT 必须在GDT 表中定义
LABEL_DESC_VIDEO:	Descriptor	  0B8000h,			  0ffffh,		  	  DA_DRW	; 显存首地址
;
; GDT 结束

; GDT 表的长度，界限，基地址
GdtLen			equ			$ - LABEL_GDT			; GDT 长度
GdtPtr			dw			GdtLen - 1				; GDT 界限
				dd			0						; GDT 基地址
				
				
; GDT 选择子的偏移地址
SelectorNormal			equ			LABEL_DESC_NORMAL	- LABEL_GDT
SelectorCode32			equ			LABEL_DESC_CODE32 	- LABEL_GDT
SelectorCode16			equ			LABEL_DESC_CODE16	- LABEL_GDT
SelectorData			equ			LABEL_DESC_DATA 	- LABEL_GDT
SelectorStack			equ			LABEL_DESC_STACK 	- LABEL_GDT
SelectorTest			equ			LABEL_DESC_TEST 	- LABEL_GDT
SelectorLdt				equ			LABEL_DESC_LDT		- LABEL_GDT
SelectorVideo			equ			LABEL_DESC_VIDEO 	- LABEL_GDT

; END of [SECTION .gdt]


[SECTION	.data1]					; 数据段
ALIGN	32					; align 地址对齐的伪指令，用来指定符号的对齐方式。align的作用范围只限于紧跟它的那条指令或者数据，而接下来的指令或者数据的地址由上一条指令的地址和其长度决定。
[BITS	32]
LABEL_DATA:

SPValueInRealMode			dw		0
; 字符串
PMMessage:					db		"In Protect Mode now. ^_^", 0
OffsetPMMessage				equ		PMMessage - $$						; 相对于 LABEL_DATA 的偏移，与段选择子一起进行索引
StrTest:					db		"ABCDEFGHIJKLMNOPQRSTUVWXYZ", 0
OffsetStrTest				equ		StrTest - $$						; 相对于 LABEL_DATA 的偏移，与段选择子一起进行索引
DataLen						equ		$ - LABEL_DATA

; END of [SECTION	.data1]	



[SECTION	.gs]				; 全局堆栈段
ALIGN	32
[BITS	32]
LABEL_STACK:
		times		512		db		0					; 填充 512 字节

TopOfStack			equ		$ - LABEL_STACK - 1			; LABEL_DESC_STACK 描述的界限

; END of [SECTION	.gs]	



[SECTION	.s16]
[BITS	16]			; BITS 指令指定 NASM 是否应生成设计为在 16 位模式、32 位模式或 64 位模式下运行的处理器上运行的代码
; 此段还在实模式，因此是 16 位
;
LABEL_BEGIN:
		; 初始化段寄存器
		mov			ax,		cs
		mov			ds,		ax
		mov			es,		ax
		mov			ss,		ax
		mov			sp,		0100h
		
		; 为回到实模式的这个跳转指令指定正确的段地址
		; jmp 指令的结构为： 低 -> 字节1(0EAh) -> 字节2-字节3(Offset) -> 字节4-字节5(Segment)
		; 因此在此时还处于实模式时，修改保护模式跳回实模式的段地址，为当前的 cs\
		; [LABEL_GO_BACK_TO_REAL + 3] 就是 jmp 结构中的 segment 部分
		mov			[LABEL_GO_BACK_TO_REAL+3], 		ax
		
		; 为回到实模式，具有正确的栈顶指针，保存栈顶指针的地址
		mov			[SPValueInRealMode],				sp
		
		
		; 初始化 32 位代码段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		cs
		shl			eax,	4
		add			eax,	LABEL_SEG_CODE32
		mov			word	[LABEL_DESC_CODE32 + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_CODE32 + 4], 	al
		mov			byte	[LABEL_DESC_CODE32 + 7],	ah
		
		; 初始化 32 位数据段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		ds
		shl			eax,	4
		add			eax,	LABEL_DATA
		mov			word	[LABEL_DESC_DATA + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_DATA + 4], 	al
		mov			byte	[LABEL_DESC_DATA + 7],	ah
		
	
		; 初始化 32 位堆栈段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		ds
		shl			eax,	4
		add			eax,	LABEL_STACK
		mov			word	[LABEL_DESC_STACK + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_STACK + 4], 	al
		mov			byte	[LABEL_DESC_STACK + 7],		ah
		
		; 初始化 16 位代码段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		cs
		shl			eax,	4
		add			eax,	LABEL_SEG_CODE16
		mov			word	[LABEL_DESC_CODE16 + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_CODE16 + 4], 	al
		mov			byte	[LABEL_DESC_CODE16 + 7],	ah

		; 初始化 LDT 在 GDT 中的描述符（补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		cs
		shl			eax,	4
		add			eax,	LABEL_LDT
		mov			word	[LABEL_DESC_LDT + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_LDT + 4], 	al
		mov			byte	[LABEL_DESC_LDT + 7],	ah

        ; 初始化 LDT 局部段描述符（补全 LDT 描述符的段基址部分）
        xor			eax,	eax
        mov			ax,		cs
        shl			eax,	4
        add			eax,	LABEL_CODE_A
        mov			word	[LABEL_LDT_DESC_CODEA + 2],	ax
        shr			eax,	16
        mov			byte	[LABEL_LDT_DESC_CODEA + 4], 	al
        mov			byte	[LABEL_LDT_DESC_CODEA + 7],	ah

		
		; 为加载 GDTR 做准备
		xor			eax,	eax
		mov			ax,		ds
		shl			eax,	4
		add			eax,	LABEL_GDT			; gdt 基地址赋 -> eax
		mov			dword	[GdtPtr + 2],	eax	; eax gdt 基地址 -> [Gdt + 2]
		
		; 加载 GDTR
		lgdt		[GdtPtr]
		
		; 关中断
		cli
		
		; 打开地址线 A20
		in			al,		92h
		or			al,		00000010b
		out			92h,	al
		
		; 准备切换到保护模式
		mov			eax,	cr0
		or			eax,	1
		mov			cr0,	eax
		
		; 真正进入保护模式
		jmp			dword	SelectorCode32:0		; 将 SelectorCode32 装入 cs，并且跳转到 SelectorCode32:0 处


LABEL_REAL_ENTRY:				; 由保护模式跳回实模式的入口
		
		mov			ax,			cs
		mov			ds,			ax
		mov			es,			ax
		mov			ss,			ax				; 恢复寄存器内容
		
		mov			sp,			[SPValueInRealMode]			; 栈顶地址
		
		in			al, 		92h	
		and			al, 		11111101b	
		out			92h, 		al				; 关闭 A20 地址线
		
		sti										; 开中断
		
		mov 		ax, 		4c00h
		int			21h							; ah=4CH — 带返回码方式的终止进程 （int 21h）


; END of [SECTION .s16]


[SECTION	.s32]			; 32 位代码段，由实模式跳入
[BITS	32]
; 此处进入保护模式，为 32 位
;
LABEL_SEG_CODE32:

		; 初始化段寄存器
		mov			ax,		SelectorData
		mov			ds,		ax					; 初始化数据段选择子
		mov			ax,		SelectorTest
		mov			es,		ax					; 初始化测试段选择子
		mov			ax,		SelectorVideo
		mov			gs,		ax					; 初始化video显存段选择子
		mov			ax,		SelectorStack
		mov			ss,		ax					; 初始化堆栈段段选择子

		mov			esp,	TopOfStack			; 栈顶地址
		
		
		; 下面显示一个字符串
		mov			ah,		0Ch						; 0000: 黑底，1100: 红字
		xor			esi,	esi
		xor			edi,	edi						; edi、esi 置零
		mov			esi,	OffsetPMMessage			; 源数据偏移赋给 esi
		mov			edi,	(80 * 10 + 0) * 2		; 目的数据偏移。屏幕第 10 行 , 第 0 列。
		; 此处设置 esi  ，为了后面调用 lodsb，edi 为了屏幕显示
		
		cld					; CLD用来操作方向标志位DF（Direction Flag）。CLD使DF复位，即DF=0
		
		
.1:					; 读取数据，并显示
		lodsb				; 汇编指令LODSB,该命令的加载方向与DF标志位有关,因此在使用此命令时需用CLD指令清DF标志位
        ;
        ; 串操作指令LODSB/LODSW/LODSD是块装入指令,
        ;     - 具体操作是把 DS:(R|E)SI 指定的内存地址中的数据读取到 AL/AX/EAX/RAX 累加寄存器中.
        ;     - 当数据载入到 AL/AX/EAX/RAX 寄存器后, (R|E)SI 寄存器将会根据 R|EFLAGS 标志寄存器的 DF 标志位,来自动增加/减少载入的数据长度
        ;         - 当DF=0时，变址寄存器SI(和DI)增加1/2/4/8
        ;         - 当DF=1时，变址寄存器SI(和DI)减少1/2/4/8
		
		test		al,			al				; 将两个操作数进行逻辑与运算，并根据运算结果设置相关的标志位，并不改变操作数1和操作数2的值，判断是否为0
		jz			.2							; 如果 al 为 0，即 zf=1，跳转到 .2 ，表示已经读取到0，读取完成
		mov			[gs:edi],	ax				; 将读取到的数据 ax 写入到 视频段，屏幕显示
		add			edi,		2				; ax 长度两个字节，每次显示两个字节
		jmp			.1
		
		
.2:					; 读取数据，并显示完成
		call		DisplayReturn

		; 此时不做函数调用，下面函数放在 LDT 中调用，函数的位置也放在了新的段中，而 DisplayReturn 还在此段中
		;call		TestRead
		;call		TestWrite
		;call		TestRead

		; 加载局部描述符 LDT
		mov         ax,         SelectorLdt
		lldt        ax							; lldt指令加载ldtr
		
		; 跳入 LDT 局部描述符所在的局部段任务
		jmp			SelectorLDTCodeA:0


; ----------------------------- SECTION	.s32 函数区 ---------------------
; =============================== DisplayRetrun
; DispReturn模拟一个回车的显示，实际上是让下一个字符显示在下一行的开头处。
; edi 保存的是下一个显示字符的位置，因此换行就是 edi 指向下一行的开头
DisplayReturn:
		push		eax
		push		ebx						; 保存 eax,ebx
		mov			eax,		edi
		mov			bl,			160
		div			bl						; edi/160(每行列数160？)，计算出当前的行数 eax
		; 商保存在 eax 中，余数保存在 edx 中。

		and			eax,		0FFh
		inc 		eax						; 换行，到下一行

		mov			bl,			160
		mul			bl						; 当前行号数 * 160，行开头位置

		mov			edi,		eax			; 置 edi
		pop			ebx
		pop			eax

		ret

; END of DisplayRetrun ===============

; ----------------------------- End of SECTION	.s32 函数区 ---------------------

SegCode32Len		equ		$ - LABEL_SEG_CODE32
; END of [SECTION .s32]



[SECTION    .ldt]           ; 局部描述符段，定义 LDT
ALIGN   32
LABEL_LDT:
; LDT 声明代码段, LDT 表项
;									            段基址,			   段界限,			属性
LABEL_LDT_DESC_CODEA:			Descriptor			0,		CodeALen - 1,	DA_C + DA_32	; LDT 表项，CODEA，32 位
;
; LDT 结束

; LDT 表的长度
LDTLen          equ         $ - LABEL_LDT


; LDT 选择子，在LDT表项中的偏移地址
; 之前 GDT 选择子没有加 TI 值，因为 GDT 就是 0，而 LDT TI 值为 1
SelectorLDTCodeA            equ         LABEL_LDT_DESC_CODEA - LABEL_LDT + SA_TIL

; END of [SECTION    .ldt]


[SECTION    .ldta]
ALIGN   32
[BITS   32]
LABEL_CODE_A:
        mov         ax,         SelectorVideo
        mov         gs,         ax                          ; gs 设置为是频段选择子

        mov         edi,        (80 * 12 + 0) * 2           ; 屏幕第 10 行, 第 0 列。
        mov         ah,         0Ch                         ; 0000: 黑底    1100: 红字
        mov         al,         'L'
        mov         [gs:edi],   ax

        ; 准备经由16位代码段跳回实模式
        jmp         SelectorCode16:0

CodeALen            equ         $ - LABEL_CODE_A

; End of [SECTION    .ldta]



[SECTION	.s16code]					; 跳回 实模式
ALIGN	32
[BITS	16]
LABEL_SEG_CODE16:
; 因为在准备结束保护模式回到实模式之前，需要加载一个合适的描述符选择子到有关段寄存器，
; 以使对应段描述符高速缓冲寄存器中含有合适的段界限和属性。
; 而且，我们不能从32位代码段返回实模式，只能从16位代码段中返回。
; 这是因为无法实现从32位代码段返回时cs高速缓冲寄存器中的属性符合实模式的要求(实模式不能改变段属性)
		
		mov			ax,			SelectorNormal
		mov			ds,			ax
		mov			es,			ax
		mov			gs,			ax
		mov			fs,			ax
		mov			ss,			ax

		mov			eax, 		cr0							; 设置 cr0 控制器 PE 位 ，进入到 实模式
		and			al, 		11111110b
		mov			cr0, 		eax

LABEL_GO_BACK_TO_REAL:	
		jmp			0:LABEL_REAL_ENTRY						; 段地址会在程序开始处被设置成正确的值

Code16Len		equ		$ - LABEL_SEG_CODE16
; END of [SECTION .s32]

```



宏定义(`OrangeOS/Protect-Mode/c/protect_mode.inc`)：

```assembly
; 描述符宏，需要3个参数
; Usage: Descriptor Base, Limit, Attr
; 		Base:		dd (段基址)
;		Limit:		dd (段界限，低 20 位有效)                  
;		Attr:		dw (属性，高字节序，低 4 位始终为 0)
%macro	Descriptor	3
		dw			%2 & 0FFFFh										; 段界限1
		dw			%1 & 0FFFFh										; 段基址1
		db			(%1 >> 16) & 0FFh								; 段基址2
		dw			((%2 >> 8) & 0F00h) | (%3 & 0F0FFh)				; 属性1 + 段界限2 + 属性2
		db			(%1 >> 24) & 0FFh								; 段基址3
%endmacro 	； 共 8 字节

; 汇编 宏：宏是一系列指令，由名称分配，可以在程序中的任何位置使用，类似一个函数。NASM 的宏格式如下：
;
; %macro macro_name  number_of_params
; 	<macro body>
; %endmacro
;
; 	- 其中，number_of_params指定数字参数， macro_name指定宏的名称
;	- 在 marco body 中，想要引用参数，可以使用 %，例如第一个参数 %1, ...
;
; 宏的使用很简单: macro_name param1, param2, ...



; 描述符类型
;----------------------------------------------------------------------------
; 描述符类型值说明
; 其中:
;       DA_  : Descriptor Attribute
;       D    : 数据段
;       C    : 代码段
;       S    : 系统段
;       R    : 只读
;       RW   : 读写
;       A    : 已访问
;       其它 : 可按照字面意思理解
;----------------------------------------------------------------------------
DA_32			EQU		4000h		; 32 位段

; DPL 位										   **	
DA_DPL0			EQU		00h			; DPL = 0(0000 0000)
DA_DPL1			EQU		20h			; DPL = 1(0010 0000)
DA_DPL2			EQU		40h			; DPL = 2(0100 0000)
DA_DPL3			EQU		60h			; DPL = 3(0110 0000)



; 存储段描述符类型
;----------------------------------------------------------------------------
; 存储段描述符类型值说明
;----------------------------------------------------------------------------
; TYPE位*							   P  S ****
DA_DR			EQU		90h			; (1001 0000) 存的是只读数据段类型值
DA_DRW			EQU		92h			; (1001 0010) 存的是可读写数据段属性值
DA_DRWA			EQU		93h			; (1001 0011) 存的是已访问可读写数据段类型值
DA_C			EQU		98h			; (1001 1000) 存的是只执行代码段属性值
DA_CR			EQU		9Ah			; (1001 1010) 存的是可执行可读代码段属性值
DA_CCO			EQU		9Ch			; (1001 1100) 存的是只执行一致代码段属性值
DA_CCOR			EQU		9Eh			; (1001 1110) 存的是可执行可读一致代码段属性值


; 系统段描述符类型
;----------------------------------------------------------------------------
; 系统段描述符类型值说明
;----------------------------------------------------------------------------
; TYPE位*							   P  S ****
DA_LDT			EQU		82h			; (1000 0010) 局部描述符表段类型值
DA_TaskGate		EQU		85h			; (1000 0101) 任务门类型值
DA_386TSS		EQU		89h			; (1000 1001) 可用 386 任务状态段类型值
DA_386CGate		EQU		8Ch			; (1000 1100) 386 调用门类型值
DA_386IGate		EQU		8Eh			; (1000 1110) 386 中断门类型值
DA_386TGate		EQU		8Fh			; (1000 1111) 386 陷阱门类型值


; 选择子最后三位表示两个标志位，TI 和 RPL
;----------------------------------------------------------------------------
;
; RPL(Requested Privilege Level): 请求特权级，用于特权检查。 （选择子的最后两位）
;
; TI(Table Indicator): 引用描述符表指示位（选择子的倒数第三位）
;	TI=0 指示从全局描述符表GDT中读取描述符；
;	TI=1 指示从局部描述符表LDT中读取描述符。
;
; 选择子类型值说明
; 其中:
;       SA_  : Selector Attribute
;----------------------------------------------------------------------------
; TI *									    *
SA_TIL			EQU		4				; (0100)	1-LDT
SA_TIG			EQU		0				; (0000)	0-GDT

; RPL *									     **
SA_RPL0			EQU		0				; (0000)	RPL - 0
SA_RPL1			EQU		1				; (0001)	RPL - 1
SA_RPL2			EQU		2				; (0010)	RPL - 2
SA_RPL3			EQU		3				; (0011)	RPL - 3
```





## LDT 说明

上例的 LDT 很简单，只有一个代码段。不难想像，我们还可以在其中增加更多的段，比如数据段、堆栈段等。这样一来，我们可以把一个单独的任务所用到的所有东西封装在一个 LDT 中。



上面代码示例与之前不同的是：

1. 在 GDT 中新定义了一个 LDT 全局描述符，因为要使用 LDT 表，必须在 GDT 中定义 LDT 描述符。并且定义了 LDT 的段选择子。

    ```assembly
    LABEL_DESC_LDT:		Descriptor			0,		  LdtLen - 1,			  DA_LDT	; LDT 段，LDT 必须在GDT 表中定义
    
    ......
    
    SelectorLdt				equ			LABEL_DESC_LDT		- LABEL_GDT
    ```

    

2. 在 LDT 中定义一个局部的 LDT 段描述符，并且定义了该局部描述符的段长度与在 LDT 中的偏移地址

    1. 注意这里在定义段选择子的偏移地址时，多出了 `SA_TIL` ，因为 TI 标志位在 LDT 和 GDT 中的值不同，LDT - TI 为 1，GDT - TI 为 0。

    ```assembly
    [SECTION    .ldt]           ; 局部描述符段，定义 LDT
    ALIGN   32
    LABEL_LDT:
    ; LDT 声明代码段, LDT 表项
    ;									            段基址,			   段界限,			属性
    LABEL_LDT_DESC_CODEA:			Descriptor			0,		CodeALen - 1,	DA_C + DA_32	; LDT 表项，CODEA，32 位
    ;
    ; LDT 结束
    
    ; LDT 表的长度
    LDTLen          equ         $ - LABEL_LDT
    
    
    ; LDT 选择子，在LDT表项中的偏移地址
    ; 之前 GDT 选择子没有加 TI 值，因为 GDT 就是 0，而 LDT TI 值为 1
    SelectorLDTCodeA            equ         LABEL_LDT_DESC_CODEA - LABEL_LDT + SA_TIL
    
    ; END of [SECTION    .ldt]
    ```

    

3. 定义LDT局部描述符  LABEL_LDT_DESC_CODEA 位置的代码，很简单，只是显示了字符 L，然后跳回实模式

    ```assembly
    [SECTION    .ldta]
    ALIGN   32
    [BITS   32]
    LABEL_CODE_A:
            mov         ax,         SelectorVideo
            mov         gs,         ax                          ; gs 设置为是频段选择子
    
            mov         edi,        (80 * 12 + 0) * 2           ; 屏幕第 10 行, 第 0 列。
            mov         ah,         0Ch                         ; 0000: 黑底    1100: 红字
            mov         al,         'L'
            mov         [gs:edi],   ax
    
            ; 准备经由16位代码段跳回实模式
            jmp         SelectorCode16:0
    
    CodeALen            equ         $ - LABEL_CODE_A
    
    ; End of [SECTION    .ldta]
    ```

    

4. 最后，在实模式开始，要初始化 LABEL_DESC_LDT 全局段描述符和初始化 LABEL_LDT_DESC_CODEA 局部段描述符的段基地址

    ```assembly
    [SECTION	.s16]
    [BITS	16]			; BITS 指令指定 NASM 是否应生成设计为在 16 位模式、32 位模式或 64 位模式下运行的处理器上运行的代码
    ; 此段还在实模式，因此是 16 位
    ;
    LABEL_BEGIN:
    
    		......
    		
    		; 初始化 LDT 在 GDT 中的描述符（补全 GDT 描述符的段基址部分）
    		xor			eax,	eax
    		mov			ax,		cs
    		shl			eax,	4
    		add			eax,	LABEL_LDT
    		mov			word	[LABEL_DESC_LDT + 2],	ax
    		shr			eax,	16
    		mov			byte	[LABEL_DESC_LDT + 4], 	al
    		mov			byte	[LABEL_DESC_LDT + 7],	ah
    
            ; 初始化 LDT 局部段描述符（补全 LDT 描述符的段基址部分）
            xor			eax,	eax
            mov			ax,		cs
            shl			eax,	4
            add			eax,	LABEL_CODE_A
            mov			word	[LABEL_LDT_DESC_CODEA + 2],	ax
            shr			eax,	16
            mov			byte	[LABEL_LDT_DESC_CODEA + 4], 	al
            mov			byte	[LABEL_LDT_DESC_CODEA + 7],	ah
    ```

    



整体流程：

1. 定义全局描述符表 GDT，其中 GDT 中有 LDT 表的位置
2. 定义 LDT 表的局部描述符表项，每一项代表一个 LDT 段
3. 定义 LDT 表项的 代码/数据 段内容
4. 在实模式开始处，初始化描述符的 段基地址（包括 GDT 和 LDT）



