# 保护模式-中断、异常



[toc]



中断、异常详细信息见 `操作系统/中断、异常和系统调用.md`



下面是 demo 程序：

```assembly
; ==========================================
; chat03 保护模式进阶
; protect_mode_test_i_1.asm
; 编译方法 nasm protect_mode_test_i_1.asm -o protect_mode_test_h.bin
; ==========================================


%include "protect_mode.inc"						; 常量、宏等说明

PageDirBase0		equ	        200000h	        ; 页目录开始地址:	2M
PageTblBase0		equ	        201000h	        ; 页表开始地址:		2M +  4K
PageDirBase1		equ	        210000h	        ; 页目录开始地址:	2M + 64K
PageTblBase1		equ	        211000h	        ; 页表开始地址:		2M + 64K + 4K

LinearAddrDemo	    equ	        00401000h
ProcFoo			    equ	        00401000h
ProcBar			    equ	        00501000h
ProcPagingDemo	    equ	        00301000h

org			0100h								; org 是 Origin 的缩写，伪指令，表示程序的起始地址
; 不是该程序在内存中的地址，而是当前程序使用的基地址

		jmp			LABEL_BEGIN


[SECTION	.gdt]
; GDT 声明代码段, GDT 表项
;									段基址,			   段界限,			属性
LABEL_GDT:			Descriptor			0,				   0,			  	   0	; 空描述符
LABEL_DESC_NORMAL:	Descriptor			0,			  0ffffh,		      DA_DRW	; Normal 描述符
LABEL_DESC_CODE32:	Descriptor			0,	SegCode32Len - 1,		DA_CR | DA_32	; 非一致性代码段，32位
LABEL_DESC_CODE16:	Descriptor			0,			  0ffffh,				DA_C	; 非一致性代码段，16位
LABEL_DESC_DATA:	Descriptor			0,		 DataLen - 1,		  	  DA_DRW	; Data 数据段
LABEL_DESC_STACK:	Descriptor			0,		  TopOfStack,	 DA_DRWA | DA_32	; Stack 栈段，32 位
LABEL_DESC_VIDEO:	Descriptor	  0B8000h,			  0ffffh,		  	  DA_DRW	; 显存首地址
; 之所以用了两个描述符来描述这个段，是因为我们不仅仅要读写这段内存，而且要执行其中的代码而这对描述符的属性要求是不一样的
LABEL_DESC_FLAT_C:  Descriptor          0,           0fffffh,   DA_CR | DA_32 | DA_LIMIT_4K     ; 0-4G 内存
LABEL_DESC_FLAT_RW: Descriptor          0,           0fffffh,   DA_DRW | DA_LIMIT_4K              ; 0-4G 内存
;
; GDT 结束

; GDT 表的长度，界限，基地址
GdtLen		    equ	        $ - LABEL_GDT	; GDT长度
GdtPtr		    dw	        GdtLen - 1	; GDT界限
		        dd	        0 		; GDT基地址


; GDT 选择子的偏移地址
SelectorNormal			equ			LABEL_DESC_NORMAL	- LABEL_GDT
SelectorCode32			equ			LABEL_DESC_CODE32 	- LABEL_GDT
SelectorCode16			equ			LABEL_DESC_CODE16	- LABEL_GDT
SelectorData			equ			LABEL_DESC_DATA 	- LABEL_GDT
SelectorStack			equ			LABEL_DESC_STACK 	- LABEL_GDT
SelectorVideo			equ			LABEL_DESC_VIDEO 	- LABEL_GDT
SelectorFlatC           equ         LABEL_DESC_FLAT_C   - LABEL_GDT
SelectorFlatRW           equ        LABEL_DESC_FLAT_RW  - LABEL_GDT



; END of [SECTION .gdt]


[SECTION	.data1]					; 数据段
ALIGN	32					; align 地址对齐的伪指令，用来指定符号的对齐方式。align的作用范围只限于紧跟它的那条指令或者数据，而接下来的指令或者数据的地址由上一条指令的地址和其长度决定。
[BITS	32]
LABEL_DATA:

; 实模式下使用这些符号

; 字符串
_PMMessage:					db		"In Protect Mode now. ^_^", 0
_szRAMSize			        db	    "RAM size:",        0               ; 内存大小字符串
_szMemChkTitle              db      "BaseAddrL BaseAddrH LengthLow LengthHigh   Type", 0Ah, 0	; 进入保护模式后显示此字符串
_szReturn                   db      0Ah,                0               ; 换行符 0Ah

; 变量
_SPValueInRealMode			dw		0
_dwMCRNumber:               db      0                                   ; 读取内存的次数，ASDR 结构体的个数
_dwMemSize:			        dd	    0                                   ; 内存大小
_dwDisplayPos:			    dd	    (80 * 6 + 0) * 2	                ; 屏幕第 6 行, 第 0 列
_PageTableNumber            dd      0                                   ; 计算出的页表个数
_ARDStruct:			                                                     ; ARDS 结构 Address Range Descriptor Structure
	    _dwBaseAddrLow:		dd	    0
        _dwBaseAddrHigh:	dd	    0
	    _dwLengthLow:		dd	    0
	    _dwLengthHigh:		dd	    0
	    _dwType:			dd	    0

_MemChkBuf:                  times       256    db  0               ; 缓冲区 256 字节

; ax=0E820h 时调用 int 15h 得到的不仅仅是内存的大小，还包括对不同内存段的一些描述。
; 这些描述都被保存在一个缓冲区中。在我们调用 int 15h 之前，必须先有缓冲
; 将把每次得到的内存信息连续写入这块缓冲区，形成一个结构体(ARDS)数组


; 保护模式下使用这些符号
; 为什么保护模式下不能直接用上面定义的变量，而是需要重新创建并且使用偏移？
; 因为程序是在实模式下编译的，地址只适用于实模式，在保护模式下，数据的地址应该是其相对于段基址的偏移。
; 也就是说上面实模式的地址都是段地址:偏移地址，而保护模式段选择子:偏移地址，因此只能用相对于当前 section 的偏移地址来确定变量位置
szRAMSize		            equ	    _szRAMSize	- $$
MemChkBuf                   equ     _MemChkBuf - $$                     ; 内存信息保存的位置，供保护模式使用
dwMCRNumber                 equ     _dwMCRNumber - $$                   ; 读取内存次数，供保护模式使用
dwMemSize		            equ	    _dwMemSize	- $$
szMemChkTitle               equ     _szMemChkTitle - $$
szReturn                    equ     _szReturn - $$                      ; 回车换行特殊字符 0Ah，共保护模式使用
dwDisplayPos                equ     _dwDisplayPos - $$
szPMMessage				    equ		_PMMessage - $$
PageTableNumber             equ     _PageTableNumber - $$
ARDStruct		            equ	    _ARDStruct	- $$
	dwBaseAddrLow	        equ	    _dwBaseAddrLow	- $$
	dwBaseAddrHigh	        equ	    _dwBaseAddrHigh	- $$
	dwLengthLow	            equ	    _dwLengthLow	- $$
	dwLengthHigh	        equ	    _dwLengthHigh	- $$
	dwType		            equ	    _dwType		- $$

DataLen						equ		$ - LABEL_DATA

; END of [SECTION	.data1]

[SECTION    .idt]
ALIGN	32
[BITS	32]
LABEL_IDT:
; IDT 声明代码段, IDT 表项
;
; 指令宏，循环：%rep，与 times 类似，但是 times 指令无法调用多行
; 循环 256 次，定义了 256 个相同的 IDT 描述符,将每一个描述符都设置为指向 SelectorCode32:SpuriousHandler 的 中 断 门
; 门                        目标选择子,            偏移,                DCount,         属性
%rep        32
            Gate            SelectorCode32,     SpuriousHandler,      0,              DA_386IGate
%endrep
; 020h = 0010 0000b = 2^5 = 32，中断向量从0开始，因此前面需要重复 128 次
.020h:      Gate            SelectorCode32,         ClockHandler,      0,              DA_386IGate

; 因为前面已经循环了 33 次，还需要 128 - 33 = 95 次
%rep        95
            Gate            SelectorCode32,     SpuriousHandler,      0,              DA_386IGate
%endrep

; 080h = 1000 0000b = 2^7 = 128，中断向量从0开始，因此前面需要重复 128 次
.080h:      Gate            SelectorCode32,     UserIntHandler80,      0,              DA_386IGate

IdtLen             equ             $ - LABEL_IDT
IdtPtr              dw             IdtLen - 1                   ; 段界限
                    dd             0                            ; 基地址


[SECTION	.gs]				; 全局堆栈段
ALIGN	32
[BITS	32]
LABEL_STACK:
		times		512		db		0					; 填充 512 字节

TopOfStack			equ		$ - LABEL_STACK - 1			; LABEL_DESC_STACK 描述的界限

; END of [SECTION	.gs]



[SECTION	.s16]
[BITS	16]			; BITS 指令指定 NASM 是否应生成设计为在 16 位模式、32 位模式或 64 位模式下运行的处理器上运行的代码
; 此段还在实模式，因此是 16 位
;
LABEL_BEGIN:
		; 初始化段寄存器
		mov			ax,		cs
		mov			ds,		ax
		mov			es,		ax
		mov			ss,		ax
		mov			sp,		0100h

		; 为回到实模式的这个跳转指令指定正确的段地址
		; jmp 指令的结构为： 低 -> 字节1(0EAh) -> 字节2-字节3(Offset) -> 字节4-字节5(Segment)
		; 因此在此时还处于实模式时，修改保护模式跳回实模式的段地址，为当前的 cs\
		; [LABEL_GO_BACK_TO_REAL + 3] 就是 jmp 结构中的 segment 部分
		mov			[LABEL_GO_BACK_TO_REAL+3], 		ax

		; 为回到实模式，具有正确的栈顶指针，保存栈顶指针的地址
		mov			[_SPValueInRealMode],				sp


		; 在实模式下，获取内存，调用 int 15h 子功能 ax=0E820h
		mov         ebx,                0                   ; 输入 ebx，后续值(continuation value)，第一次为 0
		mov         di,                 _MemChkBuf          ; 输入 es:di，会将 ARDS（Address Range Descriptor Structure）填充到此处
		; 之前已经将 es 赋值为 cs，实模式下的代码段，_MemChkBuf 也就是代码段中的偏移地址

.loop:                      ; 循环执行 int 15h，获取内存
        mov         eax,                0E820h              ; 输入 eax，功能码
        mov         ecx,                20                  ; 输入 ecx，用于限制指令填充的 ARDS 的大小，通常为 20
        mov         edx,                0534D4150h          ; 输入 edx，BIOS 将会使用此标志，对调用者将要请求的系统映像信息进行校验，这些信息会被 BIOS 放置到 `es:di` 所指向的结构中

        int         15h                                     ; 执行 15h 中断

        jc          LABEL_MEM_CHK_FAIL                      ; 当没有发生错误时，CF=0，否则 CF=1
        ; 如果 cf = 0，发生错误，跳转到 LABEL_MEM_CHK_FAIL ，结束循环

        add         di,                 20                  ; 在每一次循环进行时，寄存器di的值将会递增，每次的增量为 20 字节，因为每次读取的信息是 20 字节
        inc         dword               [_dwMCRNumber]      ; _dwMCRNumber 是一个计数器，每次循环让 _dwMCRNumber 的值加 1
        ; 到循环结束时它的值会是循环的次数，同时也是地址范围描述符结构ARDS的个数。

        cmp         ebx,                0                   ; 指向下一个内存区域，而不是调用之前的内存区域，当 ebx=0 且 CF=0 时，表示当前是最后一个内存区域。
        jne         .loop                                   ; 如果不为0,表示还没读取完成
        jmp         LABEL_MEM_CHK_OK                        ; 否则读取完成


LABEL_MEM_CHK_FAIL:                 ; 如果读取失败
        mov         dword               [_dwMCRNumber],      0   ; 将读取次数 _dwMCRNumber 复位


LABEL_MEM_CHK_OK:                   ; 如果读取成功，继续执行实模式后续代码


		; 初始化 32 位代码段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		cs
		shl			eax,	4
		add			eax,	LABEL_SEG_CODE32
		mov			word	[LABEL_DESC_CODE32 + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_CODE32 + 4], 	al
		mov			byte	[LABEL_DESC_CODE32 + 7],	ah

		; 初始化 32 位数据段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		ds
		shl			eax,	4
		add			eax,	LABEL_DATA
		mov			word	[LABEL_DESC_DATA + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_DATA + 4], 	al
		mov			byte	[LABEL_DESC_DATA + 7],	ah


		; 初始化 32 位堆栈段描述符 （补全 GDT 描述符的段基址部分）
		xor			eax,	eax
		mov			ax,		ds
		shl			eax,	4
		add			eax,	LABEL_STACK
		mov			word	[LABEL_DESC_STACK + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_STACK + 4], 	al
		mov			byte	[LABEL_DESC_STACK + 7],	ah

		; 初始化 16 位代码段描述符 （补全 GDT 描述符的段基址部分）
		mov         ax,     cs
		movzx       eax,    ax
		;xor			eax,	eax
		;mov			ax,		cs
		shl			eax,	4
		add			eax,	LABEL_SEG_CODE16
		mov			word	[LABEL_DESC_CODE16 + 2],	ax
		shr			eax,	16
		mov			byte	[LABEL_DESC_CODE16 + 4], 	al
		mov			byte	[LABEL_DESC_CODE16 + 7],	ah


		; 为加载 GDTR 做准备
		xor			eax,	eax
		mov			ax,		ds
		shl			eax,	4
		add			eax,	LABEL_GDT			; gdt 基地址赋 -> eax
		mov			dword	[GdtPtr + 2],	eax	; eax gdt 基地址 -> [Gdt + 2]

        ; 为加载 IDTR 作准备
        xor         eax,        eax
        mov         ax,         ds
        shl         eax,        4
        add         eax,        LABEL_IDT           ; idt 基地址赋 -> eax
        mov			dword	[IdtPtr + 2],	eax	    ; eax idt 基地址 -> [Gdt + 2]

		; 加载 GDTR
		lgdt		[GdtPtr]

		; 关中断，在执行lidt之前用cli指令清 IF 位暂时不响应可屏蔽中断
		cli

        ; 加载 IDTR
        lidt	    [IdtPtr]

		; 打开地址线 A20
		in			al,		92h
		or			al,		00000010b
		out			92h,	al

		; 准备切换到保护模式
		mov			eax,	cr0
		or			eax,	1
		mov			cr0,	eax

		; 真正进入保护模式
		jmp			dword	SelectorCode32:0		; 将 SelectorCode32 装入 cs，并且跳转到 SelectorCode32:0 处


LABEL_REAL_ENTRY:				; 由保护模式跳回实模式的入口

		mov			ax,			cs
		mov			ds,			ax
		mov			es,			ax
		mov			ss,			ax				; 恢复寄存器内容

		mov			sp,			[_SPValueInRealMode]			; 栈顶地址

		in			al, 		92h
		and			al, 		11111101b
		out			92h, 		al				; 关闭 A20 地址线

		sti										; 开中断

		mov 		ax, 		4c00h
		int			21h							; ah=4CH — 带返回码方式的终止进程 （int 21h）


; END of [SECTION .s16]


[SECTION	.s32]			; 32 位代码段，由实模式跳入
[BITS	32]
; 此处进入保护模式，为 32 位
;
LABEL_SEG_CODE32:
		; 初始化段寄存器
		mov			ax,		SelectorData
		mov			ds,		ax					; 初始化数据段选择子
		mov         es,     ax                  ; es 始终指向 SelectorFlatRW
		mov			ax,		SelectorVideo
		mov			gs,		ax					; 初始化video显存段选择子
		mov			ax,		SelectorStack
		mov			ss,		ax					; 初始化堆栈段段选择子

		mov			esp,	TopOfStack			; 栈顶地址

        ; 调用中断初始化程序
        call        Init8259a
        int         080h                        ; 这里无论执行哪个中断，中断的处理程序都是 SelectorCode32:SpuriousHandler
        sti                                             ; 设置 IF 可以通过指令sti来完成, IF=1，打开可屏蔽中断，这里就是时钟中断
        jmp         $

		; 显示字符串
		push        szPMMessage
		call        DisplayStr
		add         esp,     4                  ; 恢复栈顶指针，因为压入了 szPMMessage

        push	    szMemChkTitle
        call	    DisplayStr
        add	        esp,      4

        call        DisplayMemSize              ; 显示内存信息

        call        PagingDemo                  ; 演示改变页目录的效果

		; 到此停止
		jmp			SelectorCode16:0


; Init8259A 设置 8259a 中断控制----------------------------------------------------------------
; 往主、从两个 8259A 各写入了 4 个 ICW
Init8259a:
        ; 读端口用 IN 指令，写端口用 OUT 指令
        ;   - IN    AX,  21H    从端口地址21H读取1字节数据到AL，从端口地址22H读取1字节到AH
        ;   - OUT   21H, AX     将AX的值写入端口地址21H开始的连续两个字节

        ; ==== Set ICW
        mov         al,         011h                    ; 写入内容 011h = 1 0001b
        out         020h,       al                      ; 写入主 8259, ICW1.   写入内容为
        ; 第0位：1= 需要 ICW4, 0= 不需要 ICW4
        ; 第4位：对 ICW1 必须为 1（端口必须为 20h 或 A0h）
        call        io_delay

        out         0A0h,       al                      ; 写入从8259, ICW1.
        call        io_delay

        mov         al,         020h                    ; 写入内容 020h = 10 0000b
        out         021h,       al                      ; 写入主8259, ICW2
        ; 低三位 000 ：  x86 系统用不到
        ; IRQ0 对应中断向量 0x20，IRQ0∼IRQ7 就自动对应中断向量20h∼27h;
        call        io_delay

        mov         al,         028h                    ; 写入内容 028h = 10 1000
        out         0A1h,       al                      ; 写入从8259, ICW2.
        ; IRQ8 对应中断向量 0x28
        call        io_delay

        mov         al,         004h                    ; 写入内容 004h =  0100
        out         021h,       al                      ; 写入主8259, ICW3.
        ; 主片上中 ICW3 置 1 的那一位对应的 IRQ 接口用于连接从片。即 IR2 对应从8259
        call        io_delay

        mov         al,         002h                    ; 写入内容 002h =  0010
        out         0A1h,       al                      ; 写入从8259, ICW3.
        ; 从片上低三位用于表示连接到主片上的 IRQ 接口。即 002 对应主8259的 IR2
        call        io_delay

        mov         al,         001h                    ; 写入内容 001h =  0001
        out         021h,       al                      ; 写入主8259, ICW4.
        ; 最后一位 μPM 表示位处理器类型。若 μPM 为 0，表示 8080 或 8085 处理器。若 μPM 为 1，表示 x86 处理器。
        call        io_delay

        out         0A1h,       al                      ; 写入从8259, ICW4.
        call        io_delay


        ; ==== Set OCW
        mov         al,         11111110b                ; 仅仅开启定时器中断，
        ; mov         al,         11111111b                ; 屏蔽所有中断
        out         21h,        al                       ; 写入从8259, OCW1.
        ; OCW1 哪位置1,就屏蔽哪个中断，仅仅开放了 IR0，也就是时钟中断
        call        io_delay

        mov         al,         11111111b                ; 屏蔽从8259的所有中断
        out         0A1h,        al                       ; 写入从8259, OCW1.
        ; OCW1 哪位置1,就屏蔽哪个中断，仅仅开放了 IR0，也就是时钟中断
        call        io_delay

        ret
; END Init8259A 8259a 中断控制----------------------------------------------------------------

; io_delay 模拟io操作 -----
io_delay:
        nop
        nop
        nop
        nop
        ret
; END io_delay --------


; SpuriousHandler 中断处理程序 ---------------
; SpuriousHandler也很简单在屏幕的右上角打印红色的字符“!”
_SpuriousHandler:
SpuriousHandler         equ         _SpuriousHandler - $$
        mov         ah,         0Ch                 ; 0000: 黑底    1100: 红字
        mov         al,         '!'
        mov         [gs:((80 * 10 + 0) * 2)],       ax          ; 屏幕第 10 行, 第 0 列
        jmp         $
        iretd
; END SpuriousHandler

; int 080h 中断处理程序 ---------------
; 在屏幕的右上角打印红色的字符“I”s
_UserIntHandler80:
UserIntHandler80         equ         _UserIntHandler80 - $$
        mov         ah,         0Ch                 ; 0000: 黑底    1100: 红字
        mov         al,         'I'
        mov         [gs:((80 * 15 + 0) * 2)],       ax          ; 屏幕第 10 行, 第 0 列
        iretd
; END UserIntHandler80 --------------


; int 020h 时钟中断处理程序 ---------------
; 因为调用 int 080h 后，会显示在 [gs:((80 * 15 + 0) * 2)] 位置上一个 I 字符
; 时钟中断处理程序，修改 [gs:((80 * 15 + 0) * 2)] 位置上的字符，每次增加一个 ASCII 码
_ClockHandler:
ClockHandler         equ        _ClockHandler - $$
        inc          byte       [gs:((80 * 15 + 0) * 2)]        ; [gs:((80 * 15 + 0) * 2)] 修改显示位置上的字符

        mov         al,         20h                 ; 写入内容 020h = 0010 0000b
        out         20h,        al                  ; 发送 EOI 到 OCW2
        ; 写入位置是 第五位，即 EOI 标志
        ; EOI置1且其他都为0时，表示当中断处理完成后，向 8259A 发送 EOI 命令，8259A 会将 ISR 中当前级别最高的位置 0

        iretd
; END ClockHandler --------------


; 启动分页机制 --------------------------------------------------------------
; 循环将每一个 页目录 以及 页表 写入具体内存位置
SetupPaging:
        ; 根据内存大小计算应初始化多少PDE以及多少页表
        xor         edx,        edx                         ; 清空 edx
        mov         eax,        [dwMemSize]                 ; 内存大小给 eax
        mov         ebx,        400000h                     ; 400000h = 4M = 4096 * 1024, 一个页表对应的内存大小
        ; 为什么时 4M? 为了计算有多少 PTE，首先要计算一个页表占多少内存，才能用内存总大小除以一个页表大小
        ; 一个页表项指向了一个物理页的首地址，因此一个页表项对应了物理内存大小就是 4k，而一个页表有 4k 大小（4 * 1024），一共有 1024 个页表项
        ; 因此一个页表对应的物理内存的大小就是 4k * 1024 = 4M

        div         ebx                                     ; eax / ebx，总内存 / 一个页表大小 = 页表总数
        mov         ecx,        eax                         ;  ecx 为页表的个数，也即 PDE 应该的个数
        test        edx,        edx                         ; 余数是否为0
        jz	        .no_remainder                           ; 如果余数为 0 ，跳转
        inc         ecx                                     ; 否则，页表个数 + 1

.no_remainder:
        mov         [PageTableNumber],         ecx                                     ; 暂存页表个数


        ; 为简化处理, 所有线性地址对应相等的物理地址. 并且不考虑内存空洞.

        ; 首先初始化页目录
        mov         ax,         SelectorFlatRW              ; SelectorFlatRW 段选择子，段地址为 0-4G
        mov         es,         ax
        mov         edi,        PageDirBase0
        xor         eax,        eax                          ; eax 置零
        mov         eax,        PageTblBase0 | PG_P | PG_RWW | PG_USU        ; 将 PDE 的最后三位置位，表示 页存在位 P=1, 读/写/执行 R/W=1 用户级 U/S=1
        ; 此时 eax 表示 PDE 对应的页表首地址变成PageTblBase而且;属性显示其指向的是存在的可读可写的用户级别页表
.1:
        stosd                   ; STOSB、STOSW 和 STOSD 指令分别将 AL/AX/EAX 的内容存入由 EDI 中偏移量指向的内存位置。EDI 根据方向标志位的状态递增或递减。
        ; LODSB/LODSW/LODSD  内存 -> 寄存器
        ; STOSB/STOSW/STOSD  寄存器 -> 内存
        ; stosd 将 eax 中的 PageTblBase|PG_P|PG_USU|PG_RWW 存入了页目录表的第一个 PDE

        add         eax,        4096                ; 为了简化, 假设所有页表在内存中是连续的.
        ; 每一个页目录项指向的是一个页表的基地址，eax 表示的就是页目录项指向的页表地址，而一个页表的长度是 4K，因此需要每次加上 4k
        ; 这里加 4096 是因为每一个页表大小 4k，因此将所有页表都变成连续的


        loop        .1                              ; 执行 1024 次，ecx 每次循环减1

        ; 再初始化所有页表 PTE
        ; 一共 1024 个页目录，也就是有 1024 个页表
        ; 每个页表 4k 大小，每个页表项大小 4B，每个页表就有 1024 个页表项, 一共就是 1024 * 1024 个页表项，
        ; 页表一共占用 4M 内存空间
        ; 此时 es 指向 SelectorFlatRW
        mov         eax,        [PageTableNumber]            ; 获取页表个数
        mov         ebx,        1024                         ; 每个页表 1024 个 PTE
        mul         ebx                                      ; eax 表示所有 pte 个数
        mov         ecx,        eax                          ; ecx 控制循环次数
        mov         edi,        PageTblBase0
        xor         eax,        eax                          ; eax 置零
        mov         eax,        PG_P | PG_RWW | PG_USU       ; 这里假定 所有的线性地址映射到相同的物理地址
        ; 表示此 PTE 指示的页首地址为 并且是个可读可写的用户级别页
        ; 第 0 个页表中第 0 个 PTE 指示的页的首地址是,于是线性地址 0∼0FFFh 将被映射到物理地址 0∼0FFFh

.2:
        stosd
        add         eax,        4096                ; 每一页指向 4K 的空间
        loop        .2                              ; 执行 1024 * 1024 次，ecx 每次循环减1

        mov         eax,        PageDirBase0
        mov         cr3,        eax                 ; 设置 cr3 控制寄存器为页目录基地址
        mov         eax,        cr0
        or          eax,        80000000h           ; 设置 cr0 的 PG 位
        mov         cr0,        eax                 ; 设置 cr0，开启分页
        jmp         short       .3

.3:
        nop
        ret

; 分页机制启动完毕 ----------------------------------------------------------



; 测试分页机制函数 --------------------------------------------------------------
PagingDemo:
        mov             ax,             cs                      ; 代码段
        mov             ds,             ax                      ; 使用全局堆栈段
        mov             ax,             SelectorFlatRW
        mov             es,             ax                      ; SelectorFlatRW 段选择子

        push            LenFoo
        push            OffsetFoo
        push            ProcFoo
        call            MemCpy
        add             esp,            12                      ; 将 OffsetFoo 开头的内存数据，拷贝到 ProcFoo 处，并且长度为 LenFoo

        push            LenBar
        push            OffsetBar
        push            ProcBar
        call            MemCpy
        add             esp,            12                      ; 将 OffsetFoo 开头的内存数据，拷贝到 ProcFoo 处，并且长度为 LenFoo


        push            LenPagingDemoAll
        push            OffsetPagingDemoProc
        push            ProcPagingDemo
        call            MemCpy
        add             esp,            12                      ; 将 OffsetFoo 开头的内存数据，拷贝到 ProcFoo 处，并且长度为 LenFoo

        mov             ax,             SelectorData
        mov             ds,             ax
        mov             es,             ax                      ; 恢复 数据段地址，指向 data1 选择子

        call            SetupPaging                             ; 启动分页

        call            SelectorFlatC:ProcPagingDemo            ; 执行主函数，应该打印 Foo
        call            PSwitch                                 ; 切换页目录，改变地址映射关系
        call            SelectorFlatC:ProcPagingDemo            ; 再次执行主函数，应该打印 Bar

        ret


; 测试函数结束 ---------------------------------------------------------------------------


; 切换页表函数 ------------------------------------------------------------------
PSwitch:                                ; 初始化页目录，把第二个页表地址写入
        mov             ax,             SelectorFlatRW

        ; 首先初始化页目录，与之前的 SetupPaging 类似
        mov         ax,         SelectorFlatRW              ; SelectorFlatRW 段选择子，段地址为 0-4G
        mov         es,         ax
        mov         edi,        PageDirBase1	            ; 此段首地址为 PageDirBase1
        xor         eax,        eax                         ; eax 置零
        mov         eax,        PageTblBase1 | PG_P | PG_RWW | PG_USU        ; 这里配置为 PageTblBase1 页表地址
        mov         ecx,        [PageTableNumber]
.1:
        stosd
        add         eax,        4096
        loop        .1

        ; 再初始化所有页表 PTE
        mov         eax,        [PageTableNumber]            ; 获取页表个数
        mov         ebx,        1024                         ; 每个页表 1024 个 PTE
        mul         ebx                                      ; eax 表示所有 pte 个数
        mov         ecx,        eax                          ; ecx 控制循环次数
        mov         edi,        PageTblBase1
        xor         eax,        eax                          ; eax 置零
        mov         eax,        PG_P | PG_RWW | PG_USU       ; 这里假定 所有的线性地址映射到相同的物理地址

.2:
        stosd
        add         eax,        4096                ; 每一页指向 4K 的空间
        loop        .2                              ; 执行 1024 * 1024 次，ecx 每次循环减1

        ; 下面代码是为了修改 CR3 的基地址为 PageDirBase1
        ; 在此假设内存是大于 8M 的
        mov         eax,        LinearAddrDemo
        shr         eax,        22
        mov         ebx,        4096
        mul         ebx
        mov         ecx,        eax                 ; 此步骤将 页目录索引，将线性地址右移 22 位(也就是线性地址的高 10 位)，在左移 12 位，，
        ; 此时 ecx = 0001 0000 0000 0000

        mov         eax,        LinearAddrDemo
        shr         eax,        12
        and         eax,        03FFh               ; 1111111111b (10 bits)
        mov         ebx,        4
        mul         ebx                             ; 此步骤将 页表索引，将线性地址右移 12 位，在与上 03FFh(也就是线性地址的 [12,21] 位)，在左移 2 位
        ; 此时 eax = 0100

        add         eax,        ecx
        add         eax,        PageTblBase1
        ; 此时 eax = 0100 + 0001 0000 0000 0000 + PageTblBase1 = 212004h，也就是 PTE 的第一项

        mov         dword       [es:eax],           ProcBar | PG_P | PG_USU | PG_RWW            ; 将 PTE 指向的地址改称 ProcBar，打印 Bar

        ;
        mov         eax,        PageDirBase1
        mov         cr3,        eax                 ; 设置 cr3 控制寄存器为页目录基地址
        jmp         short       .3

.3:
        nop
        ret



; 具体的执行函数
PagingDemoProc:                     ; 主函数
OffsetPagingDemoProc            equ             PagingDemoProc - $$                     ; 执行的主函数，起始地址

        mov             eax,                LinearAddrDemo
        call            eax                                                             ; 调用 LinearAddrDemo 地址处的代码
        retf

LenPagingDemoAll                equ             $ - PagingDemoProc                      ; 执行主函数的长度


foo:                                ; 具体执行函数，打印 Foo
OffsetFoo                       equ             foo - $$

        mov             ah,                 0Ch                                         ; 0000: 黑底    1100: 红字
        mov             al,                 'F'                                         ; 打印字符 F
        mov             [gs:((80 * 17 + 0) * 2)],       ax                    ; 屏幕第 17 行, 第 0 列
        mov             al,                 'o'                                         ; 打印字符 o
        mov             [gs:((80 * 17 + 1) * 2)],       ax                    ; 屏幕第 17 行, 第 1 列
        mov             [gs:((80 * 17 + 2) * 2)],       ax                    ; 屏幕第 17 行, 第 2 列
        ret

LenFoo                          equ             $ - foo


bar:                                 ; 具体执行函数，打印 Bar
OffsetBar                       equ             bar - $$

        mov             ah,                 0Ch                                         ; 0000: 黑底    1100: 红字
        mov             al,                 'B'                                         ; 打印字符 F
        mov             [gs:((80 * 18 + 0) * 2)],       ax                    ; 屏幕第 17 行, 第 0 列
        mov             al,                 'a'                                         ; 打印字符 a
        mov             [gs:((80 * 18 + 1) * 2)],       ax                    ; 屏幕第 17 行, 第 1 列
        mov             al,                 'r'                                         ; 打印字符 a
        mov             [gs:((80 * 18 + 2) * 2)],       ax                    ; 屏幕第 17 行, 第 2 列
        ret

LenBar                          equ             $ - bar






; 显示内存信息函数
; 每次循环将会读取一个ARDStruct
;   - 首先打印其中每一个成员的各项
;   - 然后根据当前结构的类型，得到可以被操作系统使用的内存的上限。
;   - 结果会被存放在变量 dwMemSize 中，并在此模块的最后打印到屏幕。
DisplayMemSize:
        push            esi
        push            edi
        push            ecx                              ; 保存现场

        mov             esi,        MemChkBuf            ;
        mov             ecx,        [dwMCRNumber]        ; for (int i=0; i < [dwMCRNumber]; i++)        ecx 控制循环次数，每次得到一个ARDS
.loop:                                                   ; {
        mov             edx,        5                    ;      for (int j=0; j < 5; j++)               因为 ARDS 结构体中有五个元素
        mov             edi,        ARDStruct            ;      {   ; 依次显示 BaseAddrLow,BaseAddrHigh,LengthLow,LengthHigh,Type
.1:
        push            dword       [esi]                ;      ; ds:esi 内存内容入栈
        call            DisplayInt                       ;      DisplayInt(MemChkBuf[j*4]);     //显示一个成员
        ; DisplayInt 类似自定义库函数，需要使用到 栈指针 esp
        pop             eax                              ;       ; 出栈到 eax
        stosd                                            ;       ARDStruct[j*4] = MemChkBuf[j*4]; // 把al/ax/eax的内容存储到edi指向的内存单元中

        add             esi,        4                    ;       ; 继续 ARDS 中的下一个元素结构
        dec             edx                              ;       ; 控制内存循环
        cmp             edx,        0
        jnz             .1                               ;       }            // 内存循环，将当前指向的内存缓冲区的内容，写入到了 ARDStruct 变量中

        call            DisplayReturn                    ;      printf("\n");
        cmp             dword       [dwType],       1    ;      if (dwType == AddressRangeMemory)    // 如果内存类型是可以被os使用的内容
        jne             .2                               ;      {           ; 如果 dwType == 1
        mov             eax,        [dwBaseAddrLow]
        add             eax,        [dwLengthLow]
        cmp             eax,        [dwMemSize]          ;          if (dwBaseAddrLow + dwLengthLow > dwMemSize)
        jb              .2
        ; cmp a,b  - 如果CF标志位为1（a<b），则跳转，反之，不跳转(a>=b)
        mov             [dwMemSize],    eax              ;              dwMemSize = dwBaseAddrLow + dwLengthLow
.2:                                                      ;      }
        loop            .loop                            ; }      ; 外层循环，ecx 控制，即 dwMCRNumber

        call            DisplayReturn                    ; printf("\n");
        push            szRAMSize
        call            DisplayStr                       ; printf("RAM size:");
        add             esp,            4

        push            dword           [dwMemSize]      ;
        call            DisplayInt                       ; DisplayInt(dwMemSize);
        add             esp,            4

        pop             ecx
        pop             edi
        pop             esi                               ; 恢复现场
        ret


%include	"lib.inc"	;  自定义库函数，注意只能保护模式这里 include

SegCode32Len		equ		$ - LABEL_SEG_CODE32
; END of [SECTION .s32]


[SECTION	.s16code]					; 跳回 实模式
ALIGN	32
[BITS	16]
LABEL_SEG_CODE16:
; 因为在准备结束保护模式回到实模式之前，需要加载一个合适的描述符选择子到有关段寄存器，
; 以使对应段描述符高速缓冲寄存器中含有合适的段界限和属性。
; 而且，我们不能从32位代码段返回实模式，只能从16位代码段中返回。
; 这是因为无法实现从32位代码段返回时cs高速缓冲寄存器中的属性符合实模式的要求(实模式不能改变段属性)

		mov			ax,			SelectorNormal
		mov			ds,			ax
		mov			es,			ax
		mov			gs,			ax
		mov			fs,			ax
		mov			ss,			ax

		mov			eax, 		cr0							; 设置 cr0 控制器 PE 位 ，进入到 实模式
		and			eax, 		7FFFFFFEh
		mov			cr0, 		eax

LABEL_GO_BACK_TO_REAL:
		jmp			0:LABEL_REAL_ENTRY						; 段地址会在程序开始处被设置成正确的值

SegCode16Len		equ		$ - LABEL_SEG_CODE16
; END of [SECTION .s32]



```



说明，中断相关：

- 在 .idt 段中，定义了 20h 号中断和 80h 号中断

  - 80h 号中断，处理程序就是在屏幕上打印字符 `I`，在 `ClockHandler` 中定义
  - 20h 号中断，处理程序就是修改屏幕显示处的字符，将其 ASCII 码加一，每次时钟中断都会显示下一个 ASCII 字符，在 `UserIntHandler80` 中定义

- 在实模式下，关闭中断 cli 后，将定义好的 idt 描述符基址加载 IDTR 寄存器，然后进入保护模式

- 在保护模式下，调用中断初始化程序 `Init8259a`，对 8259a 中断控制器的编程

  - 设置 ICW（ICW1、ICW2、ICW3、ICW4），初始化中断向量

  - 设置 OCW（OCW1、OCW2、OCW3），开启时钟中断

    



